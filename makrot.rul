// Put all macros here, to avoid copypaste coding between Finnish and Swedish.
// 2024-03-14


// This rule is intented for fields with a single $e subfield.
// Useamarcon is crappy and will fail spectacularily with multiple $es.
// (= same rule is applied for all $e subfield, apparently it's always the first
// $e subfield for which the nexSub($e) check is done.
#define macro RELATORTERM | Table ('Funktiotermit.tbl'); S; \
			    If (NextSub($e) = '' Or NextSub($e) = 't' Or NextSub($e) = '0' Or NextSub($e) = '5' Or NextSub($e) = '9' ) Then RegReplace('[.]?\$', '.') \
			    Else RegReplace('[.,]?\$', ',')





#define macro 300AE | RegReplace('\\b[Nn]\\.', 'noin', 'g'); \
                      RegReplace('(\\d+) s\\.', '\\1 sivua', 'g'); \
                      RegReplace('(\\[\\d+\\]) s\\.', '\\1 sivua', 'g'); \
                      RegReplace('(\\d+) bl\\.', '\\1 blad', 'g'); \
                      RegReplace('\\bs\\. (\\d+-\\d+)', 'sivut \\1', 'g'); \
                      RegReplace('S\\. (\\d+-\\d+)', 'sivut \\1', 'g'); \
                      RegReplace('1 vol\\.', '\\1 nide', 'g'); \
                      RegReplace('(\\d+) vol\\.', '\\1 nidettä', 'g'); \
                      RegReplace('\\bsek([^A-Z-a-z])', 's\\1', 'g'); \
                      RegReplace('\\bss\\.', 'sivut', 'g'); \
                      RegReplace('\\bkuv\\.', 'kuvitettu', 'g'); \
                      RegReplace('\\bmuotokuv\\.', 'muotokuvia', 'g'); \
                      RegReplace('verkkojulkaisu', 'verkkoaineisto', 'g'); \
                      RegReplace('Verkkojulkaisu', 'Verkkoaineisto', 'g')

// Usemarcon has Mixed() but I (nv) can't get it to work
#define macro UPPERCASE130L | RegReplace('^a', 'A'); RegReplace('^b', 'B'); \
	RegReplace('^c', 'C'); RegReplace('^d', 'D'); RegReplace('^e', 'E'); \
	RegReplace('^f', 'F'); RegReplace('^g', 'G'); RegReplace('^h', 'H'); \
	RegReplace('^i', 'I'); RegReplace('^j', 'J'); RegReplace('^k', 'K'); \
	RegReplace('^l', 'L'); RegReplace('^m', 'M'); RegReplace('^n', 'N'); \
	RegReplace('^o', 'O'); RegReplace('^p', 'P'); RegReplace('^q', 'Q'); \
	RegReplace('^r', 'R'); RegReplace('^s', 'S'); RegReplace('^t', 'T'); \
	RegReplace('^u', 'U'); RegReplace('^v', 'V'); RegReplace('^w', 'W'); \
	RegReplace('^x', 'X'); RegReplace('^y', 'Y'); RegReplace('^z', 'Z');

#define macro 300BE | RegReplace('kartt\\.', 'karttoja', 'g'); \
                      RegReplace('kuv\\.', 'kuvitettu', 'g'); \
                      RegReplace('mv\\.', 'mustavalkoinen', 'g'); \
		      RegReplace('\\b1 videokas\\.', '1 videokasetti', 'g'); \
		      RegReplace('([0-9][0-9]|[02-9]) videokas\\.', '\\1 videokasettia', 'g'); \
		      RegReplace('vär\\.', 'värillinen', 'g'); \
                      RegReplace('värikuv\\.', 'värikuvitus', 'g'); \
		      RegReplace('\\b1 äänikas\\.', '1 äänikasetti', 'g'); \
		      RegReplace('([0-9][0-9]|[02-9]) äänikas\\.', '\\1 äänikasettia', 'g'); \
                      RegReplace('nuott\\.', 'nuotteja', 'g'); \
                      RegReplace('kuval\\.', 'kuvalehteä', 'g'); \
                      RegReplace('diagrs\\.', 'diagrams', 'g'); \
                      RegReplace('incl\\.', 'includes', 'g'); \
                      RegReplace('fold(\\.)* maps', 'folded maps', 'g'); \
                      RegReplace('(\\d+) irtol\\.', '\\1 irtolehteä'); \
                      Replace('kuvas.' By 'kuvasivua'); \
                      RegReplace('([^0-9]) (taul[.,])', '\\1 taulukkoja', 'g'); \
                      RegReplace('\\btaul\\.', 'taulukkoja', 'g'); \
                      RegReplace('hakem\\.|hakem(\$|\\b)', 'hakemisto', 'g'); \
                      RegReplace('lyh\\. luett\\.', 'lyhenneluettelo', 'g'); \
                      RegReplace('lähdel\\.|lähdel(\$|\\b)', 'lähdeluettelo', 'g'); \
                      RegReplace('liitel\\.|liitel(\$|\\b)', 'liiteluettelo', 'g'); \
                      RegReplace('(\\d*) taul\\.', '\\1 taulukkoa', 'g'); \
                      RegReplace('(\\d*) karttal\\.', '\\1 karttaliitettä', 'g'); \
                      RegReplace('\\] kuvas\\.|\\] kuvas(\$|\\b)', '] kuvasivua', 'g'); \
                      RegReplace('1 liites\\.|1 liites(\$|\\b)', '1 liitesivu', 'g'); \
                      RegReplace('liites\\.|liites(\$|\\b)', 'liitesivua', 'g'); \
                      RegReplace('liitt\\.', 'liitteitä', 'g'); \
                      RegReplace('[Vv]erkkokirja', 'verkkoaineisto', 'g')

#define macro 300C | RegReplace('\\b2:o', 'folio', 'g'); \
	RegReplace('\\b4:o', 'kvartto', 'g'); \
	RegReplace('\\b8:o', 'oktaavo', 'g')


// Musiikin esityskokoonpanotermien avaaminen, osakenttä $m kentissä 130, 240, 600, 610, 630, 700, 710, 730, 800, 810, 830

#define macro ESITYSKOKOONPANO | RegReplace('pianotrio', 'piano, viulu, sello'); \
                                 RegReplace('pianokvartetti', 'piano, viulu, alttoviulu, sello'); \
                                 RegReplace('pianokvintetti', 'piano, viulut (2), alttoviulu, sello'); \
                                 RegReplace('\\bork\\.|\\bork(\$|\\b)', 'orkesteri', 'g'); \
                                 RegReplace('jousiork\\.|jousiork(\$|\\b)', 'jousiorkesteri'); \
                                 RegReplace('kamariork\\.|kamariork(\$|\\b)', 'orkesteri'); \
                                 RegReplace('puhallinork\\.|puhallinork(\$|\\b)', 'puhallinorkesteri'); \
                                 RegReplace('\\bkät\\.|\\bkät(\$|\\b)', 'kätisesti'); \
                                 RegReplace('soitinääni', 'määrittämätön soitin'); \
                                 If (RegFind(S, 'sovitettu, jousitrio') >= 0) Or (RegFind(S, ', jousitrio') < 0) RegReplace('jousitrio', 'viulu, alttoviulu, sello'); \
                                 If (RegFind(S, 'sovitettu, jousikvartetti') >= 0) Or (RegFind(S, ', jousikvartetti') < 0) RegReplace('jousikvartetti', 'viulut (2), alttoviulu, sello'); \
                                 If (RegFind(S, 'sovitettu, jousikvintetti') >= 0) Or (RegFind(S, ', jousikvintetti') < 0) RegReplace('jousikvintetti', 'viulut (2), alttoviulut (2), sello'); \
                                 If (RegFind(S, 'sovitettu, puupuhallinkvartetti') >= 0) Or (RegFind(S, ', puupuhallinkvartetti') < 0) RegReplace('puupuhallinkvartetti', 'huilu, oboe, klarinetti, fagotti'); \
                                 If (RegFind(S, 'sovitettu, puhallinkvintetti') >= 0) Or (RegFind(S, ', puhallinkvintetti') < 0) RegReplace('puhallinkvintetti', 'huilu, oboe, klarinetti, käyrätorvi, fagotti')


#define macro AANITYYPPI RegReplace('\\bS\\s*(\\(\\d+)', 'sopraanot \\1', 'g'); \
  RegReplace('\\bS(,|\\s|/|\\)|\$)', 'sopraano\\1', 'g'); \
  RegReplace('\\bMz\\s*(\\(\\d+)', 'mezzosopraanot \\1', 'g'); \
  RegReplace('\\bMz(,|\\s|/|\\)|\$)', 'mezzosopraano\\1', 'g'); \
  RegReplace('\\bA\\s*(\\(\\d+)', 'altot \\1', 'g'); \
  RegReplace('\\bA(,|\\s|/|\\)|\$)', 'altto\\1', 'g'); \
  RegReplace('\\bKt\\s*(\\(\\d+)', 'kontratenorit \\1', 'g'); \
  RegReplace('\\bKt(,|\\s|/|\\)|\$)', 'kontratenori\\1', 'g'); \
  RegReplace('\\bT\\s*(\\(\\d+)', 'tenorit \\1', 'g'); \
  RegReplace('\\bT(,|\\s|/|\\)|\$)', 'tenori\\1', 'g'); \
  RegReplace('\\bBar\\s*(\\(\\d+)', 'baritonit \\1', 'g'); \
  RegReplace('\\bBar(,|\\s|/|\\)|\$)', 'baritoni\\1', 'g'); \
  RegReplace('\\bB\\s*(\\(\\d+)', 'bassot \\1', 'g'); \
  RegReplace('\\bB(,|\\s|/|\\)|\$)', 'basso\\1', 'g');



#define macro TRIOT            | RegReplace('^piano, jouset', 'piano, viulu, sello'); \
                                 RegReplace('^jouset', 'viulu, alttoviulu, sello')
                                 
#define macro KVARTETOT        | RegReplace('^piano, jouset', 'piano, viulu, alttoviulu, sello');\
                                 RegReplace('^jouset', 'viulut (2), alttoviulu, sello'); \
                                 RegReplace('^puupuhaltimet', 'huilu, oboe, klarinetti, fagotti')
                              
#define macro KVINTETOT        | RegReplace('^jouset', 'viulut (2), alttoviulut (2), sello'); \
                                 RegReplace('^piano, jouset', 'piano, viulut (2), alttoviulu, sello'); \
                                 RegReplace('^puhaltimet', 'huilu, oboe, klarinetti, käyrätorvi, fagotti')

#define macro SOVITUSMERKINTO | RegReplace('sov\\.', 'sovitettu', 'g')

#define macro RAAMATTU | RegReplace('\\bUT(\\b|\\.)', 'Uusi testamentti', 'g'); \
                         RegReplace('\\bVT(\\b|\\.)', 'Vanha testamentti', 'g')


#define macro FIKTIIVINEN_HAHMO | RegReplace('fikt\\.|fiktiivinen henkilö|fiktiv gestalt', '(fiktiivinen hahmo)', 'g');

#define macro FIKTIV_GESTALT | RegReplace('fikt\\.|fiktiivinen henkilö|fiktiivinen hahmo', '(fiktiv gestalt)', 'g');

#define macro FIKTIIVINEN_YHTEISO | RegReplace('fikt\\.', '(fiktiivinen yhteisö)')

#define macro FIKTIV_INSTITUTION | RegReplace('fikt\\.', '(fiktiv institution)')

#define macro AIKAMAAREET | RegReplace('\\bs\\.', 'syntynyt', 'g'); \
                            RegReplace('\\bk\\.', 'kuollut', 'g'); \
                            RegReplace('\\bn\\.', 'noin', 'g'); \
                            RegReplace('\\bvs\\.', 'vuosisata', 'g')

#define macro SIDOSASUT |  RegReplace('\\b(sid|Sid|SID|sidottu)(\\.|\\b)', 'kovakantinen', 'g'); \
                           RegReplace('\\b(nid|Nid|NID|nidottu)(\\.|\\b)', 'pehmeäkantinen', 'g'); \
                           RegReplace('\\bvirh\\b|\\bvirh\\.', 'virheellinen', 'g'); \
                           RegReplace('\\b(inb|inbunden)(\\b|\\.)', 'hårda pärmar', 'g'); \
                           RegReplace('\\b(hft|HFT|häftad)(\\b|\\.)', 'mjuka pärmar', 'g'); \
                           RegReplace('\\bpbk\\b|\\bpbk\\.', 'paperback', 'g'); \
                           RegReplace('\\bhbk\\b|\\bhbk\\.', 'hardback', 'g'); \
                           RegReplace('\\balk\\b|\\balk\\.', 'alkaline', 'g'); \
                           RegReplace('\\bbk\\b|\\bbk\\.', 'book', 'g'); \
                           RegReplace('\\bverkkojulk\\b|\\bverkkojulk\\.', 'verkkoaineisto', 'g')

#define macro VERKKOAINEISTO | RegReplace('paperimuodossa', 'painettuna', 'g'); \
                               RegReplace('[Pp]aperisena', 'painettuna', 'g'); \
                               RegReplace('Verkkokirja', '1 verkkoaineisto', 'g'); \
                               RegReplace('[Ii]nternet-julkaisu\\b', 'verkkoaineisto', 'g'); \
                               RegReplace('paperisena', 'painettuna', 'g'); \
                               RegReplace('[Ee]lektroninen aineisto', 'Verkkoaineisto', 'g'); \
                               RegReplace('erkkojulkaisu', 'erkkoaineisto'); \
                               RegReplace('Elektroninen julkaisu|Verkkojulkaisuun', 'Linkki verkkoaineistoon')

#define macro FUNKTIOVALIMERKIT | RegReplace('(\\)|[Mm]r\\.|[Mm]rs\\.|[Jj]r\\.|Ph\\.+\\s+D\\.|Dr\\.|Sr\\.|[Rr]ev\\.|\\s[A-Za-z]\\.|\\.[A-Za-z]\\.)($4|$e)', '\\1,\\2', 'g'); \
                                  RegReplace('\\.$e', ',$e', 'g'); \
                                  RegReplace(',,', ',')
                                  

#define macro FUNKTIOT2 | RegReplace('(\\)|[Mm]r\\.|[Mm]rs\\.|[Jj]r\\.|Ph\\.+\\s+D\\.|Dr\\.|Sr\\.|[Rr]ev\\.|\\s[A-Za-z]\\.|\\.[A-Za-z]\\.)($4|$j)', '\\1,\\2', 'g'); \
                          RegReplace('\\.($[j|4])', ',\\1', 'g'); \
                          RegReplace(',,', ',')

// Lisää loppupisteen kenttään, jos se ei lopu johonkin seuraavista merkeistä: ), ], ?, !, -, .
// NV 20230201: added rule that does not add a dot after a control subfield
#define macro LOPPUPISTE_X30 | If ( (RegFind('(\\)|\\]|\\?|\\!|-|\\.)\$') <0) And (RegFind('$[0-9][^$]+\$') <0) ) Then S + '.' Else S 

#define macro COMMA2DOT_BEFORE_L | RegReplace(',$l', '.$l');

#define macro ENTRY_FIELD_PUNCTUATION | RegReplace(',\$', ''); \
	RegReplace ('($a[^$]+),$c\\(', '\\1$c('); \
	RegReplace ('($e[^$]+[^.])\$', '\\1.') 


